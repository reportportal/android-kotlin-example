plugins {
    id 'com.android.test'
    id 'kotlin-android'
}

android {
    compileSdkVersion = rootProject.compileSdkVersion.toInteger()

    targetProjectPath ':app'

    namespace 'com.epam.test.android.espresso.junit5'
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion project.compileSdkVersion

        testInstrumentationRunner "com.epam.reportportal.android.junit5.AndroidJUnit5ReportPortalRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    packagingOptions {
        exclude 'META-INF/LICENSE*'
        exclude 'META-INF/INDEX.LIST'
    }
    testOptions {
        animationsDisabled=true
    }
}

dependencies {
    implementation project(':app') // your application project

    // Standard android libraries
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Android libraries for testing Activities
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Android Espresso libraries
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Report Portal libraries
    implementation ('com.epam.reportportal:agent-android-junit5:5.2.2') {
        exclude group: 'org.aspectj' // AspectJ is already included by Android
    }
    implementation 'com.epam.reportportal:logger-java-logback:5.2.2'

    // Logging support, newer versions of logback do not support Android
    implementation 'ch.qos.logback:logback-classic:1.3.12'

    // android-junit5 necessary libraries
    implementation 'androidx.test:runner:1.5.2'
    implementation "de.mannodermaus.junit5:android-test-core:${android_junit5_version}"
    implementation "de.mannodermaus.junit5:android-test-runner:${android_junit5_version}"

    // JUnit5 libraries, 'junit-jupiter-api' is inherited from agent
    implementation "org.junit.platform:junit-platform-runner:${project.junit5_runner_version}"
    implementation "org.junit.jupiter:junit-jupiter-engine:${junit5_version}"

    // JUnit5 (Optional) If you need "Parameterized Tests"
    implementation "org.junit.jupiter:junit-jupiter-params:${junit5_version}"
}
